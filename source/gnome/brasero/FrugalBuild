# Compiling Time: 1.39 SBU
# Maintainer: Baste <baste@frugalware.org>
# Contributor: bouleetbil <bouleetbil@frogdev.info>

pkgname=brasero
pkgver=3.6.1
pkgrel=1
pkgdesc="A CD/DVD Burning application for the GNOME desktop."
url="http://www.gnome.org/projects/brasero/"
depends=('gtk+3' 'desktop-file-utils' 'gstreamer1' 'gst1-plugins-base' 'totem-pl-parser' \
	'libnotify' 'libcanberra-gtk' 'libtracker' 'dbus-glib' 'libisofs>=1.2.4' \
	'libburn>=1.2.4' 'libsm' 'libdvdcss')
makedepends=('intltool' 'gnome-doc-utils' 'gobject-introspection' 'itstool' 'gconf')
rodepends=('cdrdao' 'cdrkit' 'dvd+rw-tools')
groups=('gnome')
conflicts=('bonfire')
replaces=('bonfire')
archs=('i686' 'x86_64')
sha1sums=('fda637ad6dd6013e197d3977a970368495fec32c')

subpkgs=("nautilus-brasero")
subdescs=('An easy to use CD/DVD burning package for Nautilus')
subdepends=("nautilus>=3.6.0")
subrodepends=("$pkgname=$pkgver")
subgroups=('gnome')
subarchs=('i686 x86_64')
subreplaces=('nautilus-cd-burner')
subprovides=('nautilus-cd-burner')

_F_gnome_doc="y"
_F_gnome_glib="y"
_F_gnome_scrollkeeper="y"
_F_gnome_desktop="n"
_F_gnome_iconcache="y"
_F_gnome_mime="y"
_F_gnome_git="n"
_F_gnome_ext=".tar.xz"
Finclude gnome gnome-scriptlet
Fconfopts+=" --enable-search --enable-playlist --enable-preview --enable-inotify --enable-libnotify --disable-schemas-install \
	--enable-caches=no --enable-introspection=yes"

if [ "$_F_gnome_git" != "n" ]; then
        unset sha1sums
fi

build() {
        if [ "$_F_gnome_git" != "n" ]; then
                Funpack_scm
                Fcd $pkgname
                sh ./autogen.sh
        else
                Fcd
        fi
	Fbuild
	Fbuild_gnome_scriptlet
	Fsplit nautilus-brasero usr/lib/nautilus
	_F_gnome_split_doc
}

# optimization OK

